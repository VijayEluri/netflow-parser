apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'distribution'
apply plugin: 'maven-publish'

group = 'net.paguo'
version = '1.0.1'
description = """netflow-app"""
ext {
    artifactBaseName = "netflow-parser"
    bintrayBaseUrl = "https://api.bintray.com/maven"
    bintrayUsername = "rsvato"
    bintrayRepository = "projects"
    bintrayPackage = "netflow-parser"
}

sourceCompatibility = 1.6
targetCompatibility = 1.6

repositories {
    mavenCentral()
}

buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'nl.javadude.gradle.plugins:license-gradle-plugin:0.5.0'
    }
}

apply plugin: "license"

dependencies {
    compile group: 'postgresql', name: 'postgresql', version:'8.1-408.jdbc3'
    compile group: 'commons-logging', name: 'commons-logging', version:'1.1'
    compile group: 'log4j', name: 'log4j', version:'1.2.12'
    testCompile group: 'junit', name: 'junit', version:'4.4'
}

license {
    header rootProject.file('src/etc/header.txt')
    strictCheck true
}

task sourcesJar(type: Jar) {
    baseName artifactBaseName
    classifier 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives sourcesJar
}

assemble.dependsOn sourcesJar


distributions {
    main {
        baseName = archivesBaseName

        contents {
            from { libsDir }
        }
    }
}

publishing {
    publications {
        plugin(MavenPublication) {
            from components.java
            artifactId 'netflow-parser'

            artifact sourcesJar

            pom.withXml {
                def root = asNode()
                root.appendNode('name', 'Netflow logs parser')
                root.appendNode('description', '')
                root.appendNode('inceptionYear', '2005')
                def license = root.appendNode('licenses').appendNode('license')
                license.appendNode('name', 'The Apache Software License, Version 2.0')
                license.appendNode('url', 'http://www.apache.org/licenses/LICENSE-2.0.txt')
                license.appendNode('distribution', 'repo')
                def developer = root.appendNode('developers')
                        .appendNode('developer')
                developer.appendNode('id', 'rsvato')
                developer.appendNode('name', 'Svyatoslav Reyentenko')
                developer.appendNode('email', 'rsvato@gmail.com')
                def scm = root.appendNode('scm')
                scm.appendNode('connection', 'scm:git:ssh://git@github.com/rsvato/netflow-parser.git')
                scm.appendNode('tag', 'HEAD')
            }
        }
    }

    repositories {
        maven {
            name "Bintray"
            url "$bintrayBaseUrl/$bintrayUsername/$bintrayRepository/$bintrayPackage"
            credentials {
                username = bintrayUsername
                password = bintrayApiKey
            }
        }
    }
}
